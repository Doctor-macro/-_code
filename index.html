<!doctype html>
<html lang="ja">
<head>
<meta charset="utf-8">
<title>VBA Code Share</title>
<style>
  body { font-family: Consolas, monospace; padding: 20px; }
  pre {
    background: #f5f5f5;
    padding: 15px;
    border-radius: 6px;
    overflow-x: auto;
  }
</style>
</head>
<body>
<h2>VBA Code</h2>
<pre><code>

Option Explicit

'========================
'  共通：結合セル対応取得
'========================
Private Function GetMergedText(ByVal rng As Range) As String
    If rng Is Nothing Then
        GetMergedText = ""
        Exit Function
    End If

    If rng.MergeCells Then
        GetMergedText = CStr(rng.MergeArea.Cells(1, 1).Value)
    Else
        GetMergedText = CStr(rng.Value)
    End If
End Function

Private Function NzStr(ByVal v As Variant) As String
    If IsError(v) Then
        NzStr = ""
    ElseIf IsEmpty(v) Or v = "" Then
        NzStr = ""
    Else
        NzStr = CStr(v)
    End If
End Function

'========================
'  結合セル左上行判定
'========================
Private Function IsMergeTopLeftRow(ByVal c As Range) As Boolean
    If c.MergeCells Then
        IsMergeTopLeftRow = (c.Row = c.MergeArea.Row) And (c.Column = c.MergeArea.Column)
    Else
        IsMergeTopLeftRow = True
    End If
End Function

Private Function NextWriteRow(ByVal wsOut As Worksheet) As Long
    Dim lastA As Long
    lastA = wsOut.Cells(wsOut.Rows.Count, "A").End(xlUp).Row
    If lastA < 4 Then
        NextWriteRow = 4
    Else
        NextWriteRow = lastA + 1
    End If
End Function

'=========================================================
'  配属店舗一覧：1行出力共通
'=========================================================
Private Sub WriteHaisyokuRow( _
    ByVal wsOut As Worksheet, _
    ByVal fileBase As String, _
    ByVal sheetB1 As String, _
    ByVal dept1 As String, _
    ByVal dept2 As String, _
    ByVal dept3 As String, _
    ByVal dept4 As String, _
    ByVal shopName As String, _
    ByVal empNo As String, _
    ByVal label As String, _
    ByVal pos As String)

    If Trim$(empNo) = "" And Trim$(label) = "" And Trim$(pos) = "" Then Exit Sub

    Dim outR As Long
    outR = NextWriteRow(wsOut)

    wsOut.Cells(outR, "A").Value = sheetB1
    wsOut.Cells(outR, "B").Value = fileBase
    ' C列は手動編集用

    wsOut.Cells(outR, "D").Value = dept1
    wsOut.Cells(outR, "F").Value = dept2
    wsOut.Cells(outR, "G").Value = dept3
    wsOut.Cells(outR, "H").Value = dept4

    wsOut.Cells(outR, "I").Value = shopName
    wsOut.Cells(outR, "J").Value = empNo
    wsOut.Cells(outR, "K").Value = label
    wsOut.Cells(outR, "L").Value = pos
End Sub

'=========================================================
'  メイン
'=========================================================
Public Sub 取り込み実行_本社_配属店舗一覧()
    Dim wsOut As Worksheet
    Set wsOut = ActiveSheet

    Dim fd As FileDialog
    Set fd = Application.FileDialog(msoFileDialogFilePicker)

    With fd
        .AllowMultiSelect = True
        .Filters.Clear
        .Filters.Add "Excel", "*.xlsx;*.xlsm;*.xls"
        If .Show <> -1 Then Exit Sub
    End With

    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.Calculation = xlCalculationManual

    Dim i As Long
    Dim wbSrc As Workbook

    For i = 1 To fd.SelectedItems.Count
        Set wbSrc = Workbooks.Open(fd.SelectedItems(i), ReadOnly:=True)
        Extract_Honsya wbSrc, wsOut
        Extract_Haisyoku wbSrc, wsOut
        wbSrc.Close False
    Next i

    Application.Calculation = xlCalculationAutomatic
    Application.EnableEvents = True
    Application.ScreenUpdating = True

    MsgBox "取り込み完了"
End Sub

'========================
'  【本社】
'========================
Private Sub Extract_Honsya(ByVal wbSrc As Workbook, ByVal wsOut As Worksheet)
    Dim ws As Worksheet
    On Error Resume Next
    Set ws = wbSrc.Worksheets("本社")
    On Error GoTo 0
    If ws Is Nothing Then Exit Sub

    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.Count, "G").End(xlUp).Row

    Dim r As Long, tier As Long
    Dim baseCol As Long
    Dim deptName(1 To 5) As String
    Dim pos As String, empNo As String, empName As String
    Dim honsyaD As String
    Dim fileBase As String: fileBase = wbSrc.Name

    For r = 5 To lastRow
        honsyaD = Trim$(GetMergedText(ws.Cells(r, "D")))

        For tier = 1 To 5
            baseCol = 7 + (tier - 1) * 5
            deptName(tier) = Trim$(GetMergedText(ws.Cells(r, baseCol + 1)))
        Next tier

        For tier = 1 To 5
            baseCol = 7 + (tier - 1) * 5
            pos = NzStr(ws.Cells(r, baseCol + 2).Value)
            empNo = NzStr(ws.Cells(r, baseCol + 3).Value)
            empName = NzStr(ws.Cells(r, baseCol + 4).Value)

            If empNo <> "" Or empName <> "" Or pos <> "" Then
                Dim outR As Long: outR = NextWriteRow(wsOut)
                wsOut.Cells(outR, "A").Value = honsyaD
                wsOut.Cells(outR, "B").Value = fileBase
                wsOut.Cells(outR, "D").Resize(1, 5).Value = deptName
                wsOut.Cells(outR, "J").Value = empNo
                wsOut.Cells(outR, "K").Value = empName
                wsOut.Cells(outR, "L").Value = pos
            End If
        Next tier
    Next r
End Sub

'========================
'  【配属店舗一覧】
'========================
Private Sub Extract_Haisyoku(ByVal wbSrc As Workbook, ByVal wsOut As Worksheet)
    Dim ws As Worksheet
    On Error Resume Next
    Set ws = wbSrc.Worksheets("配属店舗一覧")
    On Error GoTo 0
    If ws Is Nothing Then Exit Sub

    Dim lastRow As Long
    lastRow = ws.Cells(ws.Rows.Count, "AN").End(xlUp).Row

    Dim r As Long
    Dim dept1 As String, dept2 As String, dept3 As String, dept4 As String
    Dim shopName As String
    Dim sheetB1 As String
    Dim tmpB As String
    Dim fileBase As String: fileBase = wbSrc.Name

    Dim posCols(1 To 5) As Long, startCols(1 To 5) As Long
    posCols(1) = ws.Columns("AQ").Column: startCols(1) = ws.Columns("AT").Column
    posCols(2) = ws.Columns("AW").Column: startCols(2) = ws.Columns("AZ").Column
    posCols(3) = ws.Columns("BC").Column: startCols(3) = ws.Columns("BF").Column
    posCols(4) = ws.Columns("BI").Column: startCols(4) = ws.Columns("BL").Column
    posCols(5) = ws.Columns("BO").Column: startCols(5) = ws.Columns("BR").Column

    For r = 5 To lastRow
        dept1 = GetMergedText(ws.Cells(r, "Q"))
        dept2 = GetMergedText(ws.Cells(r, "W"))
        dept3 = GetMergedText(ws.Cells(r, "AC"))
        dept4 = GetMergedText(ws.Cells(r, "AI"))
        shopName = NzStr(ws.Cells(r, "AO").Value)

        tmpB = GetMergedText(ws.Cells(r, "B"))
        sheetB1 = IIf(Len(tmpB) > 0, Left$(tmpB, 1), "")

        Dim blk As Long
        For blk = 1 To 5
            WriteHaisyokuRow wsOut, fileBase, sheetB1, dept1, dept2, dept3, dept4, shopName, _
                NzStr(ws.Cells(r, startCols(blk)).Value), _
                NzStr(ws.Cells(r, startCols(blk) + 1).Value), _
                NzStr(ws.Cells(r, posCols(blk)).Value)
        Next blk

        ' 追加役職（左上行のみ）
        If IsMergeTopLeftRow(ws.Cells(r, "T")) Then
            WriteHaisyokuRow wsOut, fileBase, sheetB1, dept1, "", "", "", "", _
                NzStr(ws.Cells(r, "T").Value), NzStr(ws.Cells(r, "U").Value), "総括部長/本部長"
        End If

        If IsMergeTopLeftRow(ws.Cells(r, "Z")) Then
            WriteHaisyokuRow wsOut, fileBase, sheetB1, dept1, dept2, "", "", "", _
                NzStr(ws.Cells(r, "Z").Value), NzStr(ws.Cells(r, "AA").Value), "部長/UL"
        End If

        If IsMergeTopLeftRow(ws.Cells(r, "AF")) Then
            WriteHaisyokuRow wsOut, fileBase, sheetB1, dept1, dept2, dept3, "", "", _
                NzStr(ws.Cells(r, "AF").Value), NzStr(ws.Cells(r, "AG").Value), "AMGR/CMGR"
        End If

        If IsMergeTopLeftRow(ws.Cells(r, "AL")) Then
            WriteHaisyokuRow wsOut, fileBase, sheetB1, dept1, dept2, dept3, dept4, shopName, _
                NzStr(ws.Cells(r, "AL").Value), NzStr(ws.Cells(r, "AM").Value), "MGR"
        End If
    Next r
End Sub



                          
</code></pre>
</body>
</html>
